buildscript {
    apply from: "https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle", to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-dependencies.gradle', to: buildscript
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.38.0'
}

group = 'org.jenkins-ci.plugins'
version = '2.4.0-SNAPSHOT'
description = 'Allows users to run Coverity commands in their builds'

apply plugin: 'com.synopsys.integration.solution'

def jcenterRepo = project.repositories.findByName('BintrayJCenter')
project.repositories.remove(jcenterRepo)
def mavenRepo = project.repositories.findByName('maven')
project.repositories.remove(mavenRepo)

project.afterEvaluate {
    // MavenLocal is added by the gradle-jpi-plugin and the common-gradle-plugin so we have to remove it after the plugins are evaluated
    List<String> repoNames = new ArrayList()
    repoNames.addAll(project.repositories.getNames())
    for (String repoName : repoNames) {
        if (repoName.contains('MavenLocal')) {
            def repoToRemove = project.repositories.findByName(repoName)
            project.repositories.remove(repoToRemove)
        }
    }
}

repositories {
    maven { url 'https://sig-repo.synopsys.com/bds-integrations-release' }
}

artifactory {
    publish {
        defaults { publications('mavenJpi') }
    }
}


jenkinsPlugin {
    // Must be compatible with the version declared in Jenkins Common
    coreVersion = '2.60.1'
    displayName = 'Synopsys Coverity plugin'
    url = 'https://github.com/jenkinsci/synopsys-coverity-plugin'
    gitHubUrl = 'https://github.com/jenkinsci/synopsys-coverity-plugin'

    compatibleSinceVersion = '2.0.0'
    pluginFirstClassLoader = false
}

dependencies {
    annotationProcessor 'com.synopsys.integration:jenkins-annotation-processor:0.0.1'

    implementation 'com.synopsys.integration:coverity-common:0.6.0'
    implementation 'com.synopsys.integration:jenkins-common:0.1.10'

    // Must be compatible with the versions declared in Jenkins Common
    jenkinsPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    jenkinsPlugins 'org.jenkins-ci.plugins:plain-credentials:1.0'

    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl:1.67'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.9'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.23'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.10'
}
